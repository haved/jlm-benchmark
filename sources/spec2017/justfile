cpu2017-dir := "cpu2017"
default_path_to_cpu2017_tar_xz := "~/Development/cpu2017.tar.xz"

default: run-cpu2017

install-cpu2017 path_to_cpu2017_tar_xz=default_path_to_cpu2017_tar_xz:
    #!/usr/bin/bash
    set -euxo pipefail
    if [ ! -d {{cpu2017-dir}} ]; then
        mkdir {{cpu2017-dir}}
        mkdir {{cpu2017-dir}}/install_archives
        ln -s `readlink -f {{path_to_cpu2017_tar_xz}}` {{cpu2017-dir}}/install_archives/cpu2017.tar.xz
        cd {{cpu2017-dir}}
        tar -xvf install_archives/cpu2017.tar.xz \
            tools/bin/linux-x86_64 \
            bin/harness/runcpu \
            bin/scripts.misc/exec_test \
            install.sh

        ./install.sh -f
    fi

install-configs:
    cp -r --update=all config {{cpu2017-dir}}

run-cpu2017 config="clang-config": install-cpu2017 install-configs
    #!/usr/bin/bash
    cd {{cpu2017-dir}}
    source shrc
    set -euxo pipefail

    # In case we use the clang-config, set this environment variable
    export LLVM_BIN_PATH=`llvm-config-18 --bindir`

    runcpu --config={{config}} --size=test --copies=1 --noreportable --iterations=1 \
        502.gcc \
        505.mcf \
        507.cactuBSSN \
        525.x264 \
        526.blender \
        538.imagick \
        557.xz \
        544.nab \
        999.specrand

terminate-cpu2017:
    killall specperl

clean-old-build-folders:
    rm {{cpu2017-dir}}/benchspec/CPU/*/build -r

clean-old-run-folders:
    rm {{cpu2017-dir}}/benchspec/CPU/*/run -r

# Cleans up run and build folders, but leaves the installation extracted
clean: clean-old-build-folders clean-old-run-folders

# Removes the entire installation
purge-cpu2017:
    rm -rf {{cpu2017-dir}}
